function gcdx_(a::T, b::T) where T 
    u, v = one(T), zero(T) # - универсальнее, чем 1, 0 и гарантирует стабильность типов переменных
    u_, v_ = v, u
    # a0, b0 = a, b
    #ИНВАРИАНТ:
    # НОД(a,b) = HОД(a0,b0) && a = u*a0 + v*b0 && b = u_*a0 + v_ * b0
    while !iszero(b) # - это условие более универсальное, чем b != 0 или b > 0. Функция iszero определена для всех числовых типов. Для пользовательских типов ее надо будет определять
        k, r = divrem(a,b) # remdiv(a,b) возвращает кортеж из rem(a,b) и div(a,b)
        a, b = b, r # r = a - k*b
        u, u_ = u_, u-k*u_ # эти преобразования переменных следуют из инварианта цикла
        v, v_ = v_, v-k*v_
    end
    if a < 0
        a, u, v = -a, -u, -v
    end
    return a, u, v #1-НОД, 2-коэффицент перед a, 3-коэффицент перед b
end
